PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX : <http://data.dzl.de/ont/dwh#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX prov: <http://www.w3.org/ns/prov#> 
PREFIX cs: <http://purl.org/vocab/changeset/schema#> 
SELECT DISTINCT ?concept ?notation ?newnotation 
WHERE { 
	{
		SELECT ?concept ?notation ?newnotation
		WHERE {	
			?usage cs:removal [ 
				a rdf:Statement; 
				rdf:subject ?oldconcept; 
				rdf:predicate skos:notation; 
				rdf:object ?notation 
			] . 
			?concept skos:notation ?newnotation . 
			?concept prov:wasDerivedFrom+ ?oldconcept . 
			FILTER NOT EXISTS { ?concept skos:notation ?notation } 
		}
	}
	UNION
	{
		SELECT ?concept ?notation ?newnotation
		WHERE {	
			?concept skos:notation ?newnotation .
			?usage cs:removal [ 
				a rdf:Statement; 
				rdf:subject ?concept; 
				rdf:predicate skos:notation; 
				rdf:object ?notation 
			] . 			
		}
	}
	FILTER (?notation in (<NOTATIONS>)) 
	FILTER NOT EXISTS { ?someconcept skos:notation ?notation }
	FILTER (?notation != ?newnotation)
}